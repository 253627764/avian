buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.0'
  }
}

apply plugin: 'native-component'
apply plugin: 'ivy-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'java'

artifactory {
  contextUrl = "http://oss.jfrog.org"

  resolve {
    repository {
      repoKey = 'libs-releases'
    }
  }

  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = System.env.BINTRAY_USER
      password = System.env.BINTRAY_API_KEY
    }

    defaults {
      //publications('linux-x86_64')
    }
  }
}

model {
  platforms {
    create("linux-x86_64") {
      operatingSystem "linux"
      architecture "x86_64"
    }
  }
}

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.6"
  targetCompatibility = "1.6"

  options.with {
    encoding = "UTF-8"
    bootClasspath = sourceSets.main.output.classesDir
  }
}

sourceSets {
  main {
    java {
      srcDir 'classpath'
    }
    resources {
      srcDir 'classpath'
    }
  }
}

publishing {
  repositories {
    ivy {
      url "${System.env.HOME}/.ivy2/local"
      layout 'maven'
    }
  }

  publications {

    platforms.each { platform ->
      create(platform.name, IvyPublication) {
        def nativeBuildDir = "${buildDir}/${platform.operatingSystem.name}-${platform.architecture.name}"

        module "${project.name}-${platform.name}"

        artifact("${nativeBuildDir}/avian") {
          name "avian"
          type "bin"
          extension "bin"
        }

        artifact("${nativeBuildDir}/classpath.jar") {
          name "classpath"
          type "jar"
        }

        artifact("${nativeBuildDir}/binaryToObject/binaryToObject") {
          name "binaryToObject"
          type "bin"
          extension "bin"
        }

        artifact("${nativeBuildDir}/libavian.a") {
          name "libavian"
          type "a"
          extension "a"
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}